buildscript {
    repositories {
        mavenCentral()

        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://repo.spongepowered.org/maven/' }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:4.+'
        classpath 'org.spongepowered:mixingradle:0.7.+'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.0'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'idea'

apply plugin: 'org.spongepowered.mixin'

version = '1.0.0'
group = 'com.paragon'

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.encoding 'UTF-8'
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.spongepowered.org/maven/' }
    maven { url 'https://jitpack.io' }
}

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        client {
            workingDirectory project.file('run')

            property 'fml.coreMods.load', 'com.paragon.MixinLoader'

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

configurations {
    libraries
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    libraries('org.spongepowered:mixin:0.8.5') {
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    annotationProcessor('org.spongepowered:mixin:0.8.5:processor') {
        exclude module: 'gson'
    }

    libraries group: 'org.json', name: 'json', version: '20220320'
    libraries group: 'com.github.Litarvan', name: 'OpenAuth', version: '1.1.2'
    libraries group: 'club.minnced', name: 'java-discord-rpc', version: '2.0.1'
    libraries group: 'com.github.therealbush', name: 'translator', version: '1.0.2'
    libraries group: 'com.github.Wolfsurge', name: 'animationsystem', version: '1.0'

    libraries('org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10') {
        exclude module: 'kotlin-stdlib-common'
        exclude module: 'annotations'
    }

    libraries('org.jetbrains.kotlin:kotlin-reflect:1.7.10') {
        exclude module: 'kotlin-stdlib'
    }

    libraries('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4') {
        exclude module: 'kotlin-stdlib-jdk8'
        exclude module: 'kotlin-stdlib-common'
    }

    implementation configurations.libraries
}

mixin {
    defaultObfuscationEnv 'searge'

    add sourceSets.main, 'mixins.paragon.refmap.json'
    config 'mixins.paragon.json'
}

processResources {
    from(sourceSets.main.resources) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        include 'mcmod.info'
        expand version: version, 'mcversion': '1.12.2'
    }
}

task prepareAssets(type: Copy) {
    group = 'build'
    from project.file('src/main/resources')
    into project.file('build/classes/kotlin/main')
}

jar {
    manifest.attributes(
            'Manifest-Version': 1.0,
            'MixinConfigs': 'mixins.paragon.json',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'FMLCorePluginContainsFMLMod': 'true',
            'FMLCorePlugin': 'com.paragon.asm.MixinLoader',
            'ForceLoadAsMod': 'true'
    )

    from {
        configurations.libraries.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

// Kotlin plugin doesn't seem to automatically do this (at least for me)
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}